apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "de.cineaste.android"
        minSdkVersion 19
        targetSdkVersion sdk_version
        versionCode 15
        versionName "2.6.1"
        resValue "string", "app_name", "Cineaste"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "Cineaste-Debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "androidx.legacy:legacy-support-v4:$ax_version"
    implementation "androidx.palette:palette:$ax_version"
    implementation "androidx.cardview:cardview:$ax_version"
    implementation "androidx.recyclerview:recyclerview:$ax_version"
    implementation "com.google.android.material:material:$ax_version"
    implementation 'com.google.android.gms:play-services-nearby:16.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.github.markusfisch:ScalingImageView:1.1.2'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    testImplementation 'junit:junit:4.12'
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
